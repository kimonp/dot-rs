/// This file derived from the work of Martin Vassor:
/// * <https://framagit.org/Bromind/dot-parser/>
/// And:
/// * <https://graphviz.org/doc/info/lang.html>
dotgraph = { strict? ~ (graph | digraph) ~ ident? ~ "{" ~ stmt_list ~ "}" }

stmt_list = { ( stmt ~ ";"? ~ stmt_list )? }
stmt = { subgraph | edge_stmt | attr_stmt | id_eq | node_stmt }

attr_stmt = { (graph | node | edge) ~ attr_list }
attr_list = { "[" ~ a_list ~ "]" ~ attr_list? }
a_list = { ident ~ "=" ~ ident ~ ( ";" | "," )? ~ a_list? }

edge_stmt = { (node_id) ~ edge_rhs ~ attr_list? }
edge_rhs = { edgeop ~ (node_id) ~ edge_rhs? }
edgeop = _{ "--" | "->" }

node_stmt = { node_id ~ attr_list? }
node_id = { ident ~ ( port )? }

port = {
	 ":" ~ compass_pt
	| ":" ~ ident ~ ( ":" ~ compass_pt)?
	}

subgraph = { "subgraph" ~ ident? ~ "{" ~ stmt_list ~ "}" }

compass_pt = { n | ne | e | se | s | sw | w | nw | c | underscore }

graph = { "graph" }
node = { "node" }
edge = { "edge" }
strict = { "strict" }
digraph = { "digraph" }

ident = @{ ident1 | numeral | quote | html }
ident1 = _{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
numeral = _{ "-"? ~ ( ("." ~ ASCII_DIGIT+) | ( ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? ) ) }

quote = _{ "\"" ~ ( ("\\\"") | not_quote)+ ~ "\"" }
not_quote = { !("\"") ~ ANY }

html = _{ "<" ~ not_lt_gt+ ~ ">" }
not_lt_gt = { !("<" | ">") ~ ANY }

id_eq = { ident ~ "=" ~ ident }

n = { "n" }
ne = { "ne" }
e = { "e" }
se = { "se" }
s = { "s" }
sw = { "sw" }
w = { "w" }
nw = { "nw" }
c = { "c" }
underscore = { "_" }

WHITESPACE = _{ " " | "\t" | NEWLINE }